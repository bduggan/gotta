#!/usr/bin/env perl
use Mojolicious::Lite;
use Data::Dumper;
use v5.20;
use experimental 'signatures';
use Mojo::Util qw/trim/;

plugin 'config';

our %tasks;
helper tasks => sub($c) {
    # name => { fulltext, fullurl }
    unless (%tasks) {
        my $url = Mojo::URL->new('http://rosettacode.org/mw/index.php')->query(
            'title'          => 'Special:Ask',
            'sort[0]'        => 'Modification_date',
            'p[sort]'        => 'Modification_date',
            'q'              => '[[Is task::true]]',
            'p[format]'      => 'json',
            'p[headers]'     => 'show',
            'p[limit]'       => '500',
            'order[0]'       => 'DESC',
            'p[link]'        => 'all',
            'order_num'      => 'ASC'
       );
       my $tx = $c->ua->get($url);
       my $got = $tx->res->json;
       %tasks = %{ $got->{results} };
   }
   \%tasks;
};

helper task_names => sub($c) {
    return sort keys %{ $c->tasks };
};

helper task => sub($c,$name) {
    return $c->tasks->{$name};
};

get '/' => sub($c) {
    $c->render(template => 'index');
};

get '/task/*name' => sub($c) {
    my $task = $c->stash('name');
    my $page = $c->ua->get($c->task($task)->{fullurl})->res;
    my @langs = $page->dom->find('span.mw-headline')
        ->map(sub { shift->attr('id') })->each;
    $c->render(template => 'task', langs => \@langs);
} => 'task';

get '/lang/:lang/:task' => [ lang => '\d+' ] => sub($c) {
    my $task = $c->stash('task');
    my $url = Mojo::URL->new('http://rosettacode.org/mw/index.php')
        ->query(title => $task, action => 'edit', section => $c->stash('lang'));
    my $page = $c->ua->get($url)->res;
    my $markdown = $page->dom->at('textarea#wpTextbox1')->text;
    $markdown =~ /<lang ([^>]+)>(.*)<\/lang>/mxs;
    my $language = $1;
    my $code = $2;
    $code =~ s/^(.*) \{\{ out \}\} .*$ /$1/mxs;
    my $glots = $c->ua->get('https://run.glot.io/languages/')->res->json;
    $glots = [ map [ $_->{name} => $_->{url} ], @$glots ];
    $c->render(template => 'langtask', code => $code, page => $page, language => $language, glots => $glots);
} => 'langtask';

# thx: https://github.com/zoffixznet/perl6.party/blob/master/app.pl
post '/run' => sub {
    my $c = shift;
    my $code = $c->param('code')
        or return $c->reply->not_found;
    $code =~ s/\N{ZERO WIDTH SPACE}//g;
    say 'sending';
    say $code;
    say '---------';
    my $glot_url = $c->param('glot');
    $c->ua->post(
        "$glot_url/latest" => {
            'Content-type'  => 'application/json',
            'Authorization' => 'Token ' . $c->config('glot_key')
        } => json => { files => [{ name => 'runme.rb', content => $code }] }
        => sub {
            my ($ua, $tx) = @_;
            say $tx->res->to_string;
            my $out = $tx->res->json;
            $out = trim join "\n", $out->{stdout} // (),
                $out->{stderr} ? "STDERR:\n$out->{stderr}" : ();
            $c->res->headers->content_type('text/plain');
            $c->render( text => $out );
        },
    );

    $c->render_later;
};



app->start;
__DATA__

@@ index.html.ep
% layout 'default';
% title 'Gletta';
<ol>
% for my $t (task_names) {
<li><%= link_to 'task', { name => $t } => begin %><%= $t %><%= end %>
% }
</ol>
@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head><title><%= title %></title></head>
  <body><%= content %></body>
</html>

@@ task.html.ep
% layout 'default';
<h1><%= $name %></h1>
<a target="_blank" href="<%= task($name)->{fullurl} %>">rosettacode</a>
<ol>
% my $i = 0;
% for my $l (@$langs) {
<li><%= link_to 'langtask' => { lang => ++$i, task => $name } => begin %><%= $l %><%= end %>
% }
</ol>

@@ langtask.html.ep
% layout 'default';
<h1><%= $task %></h1>
<h2><%= $language %></h2>
%= form_for 'run', method => 'POST' => begin
<pre>
%= select_field 'glot', $glots
</pre>
<textarea name='code' style='border:1px solid black;' rows=25 cols=80>
%= $code
</textarea>
<br>
%= submit_button 'run'
%= end

