#!/usr/bin/env perl
use Mojolicious::Lite;
use Data::Dumper;
use v5.20;
use experimental 'signatures';
use Mojo::Util qw/trim/;

plugin 'config';

our %tasks;
helper tasks => sub($c) {
    # name => { fulltext, fullurl }
    unless (%tasks) {
        my $url = Mojo::URL->new('http://rosettacode.org/mw/index.php')->query(
            'title'          => 'Special:Ask',
            'sort[0]'        => 'Modification_date',
            'p[sort]'        => 'Modification_date',
            'q'              => '[[Is task::true]]',
            'p[format]'      => 'json',
            'p[headers]'     => 'show',
            'p[limit]'       => '500',
            'order[0]'       => 'DESC',
            'p[link]'        => 'all',
            'order_num'      => 'ASC'
       );
       my $tx = $c->ua->get($url);
       my $got = $tx->res->json;
       %tasks = %{ $got->{results} };
   }
   \%tasks;
};

helper task_names => sub($c) {
    return sort keys %{ $c->tasks };
};

helper task => sub($c,$name) {
    return $c->tasks->{$name};
};

helper glots => sub($c) {
    my $cached;
    $cached //= do {
        my $glots = $c->ua->get('https://run.glot.io/languages/')->res->json;
        [ map [ $_->{name} => $_->{url} ], @$glots ];
    };
    $cached;
};

get '/' => sub($c) {
    $c->render(template => 'index');
};

get '/task/*name' => sub($c) {
    my $task = $c->stash('name');
    my $page = $c->ua->get($c->task($task)->{fullurl})->res;
    my @langs = $page->dom->find('span.mw-headline')
        ->map(sub { shift->attr('id') })->each;
    $c->render(template => 'task', langs => \@langs);
} => 'task';

get '/lang/:lang/*task' => [ lang => '\d+' ] => sub($c) {
    my $task = $c->stash('task');
    my $url = Mojo::URL->new('http://rosettacode.org/mw/index.php')
        ->query(title => $task, action => 'edit', section => $c->stash('lang'));
    my $page = $c->ua->get($url)->res;
    my $markdown = $page->dom->at('textarea#wpTextbox1')->text;
    $markdown =~ /<lang ([^>]+)>(.*?)\<\/lang\>/mxs;
    my $language = $1;
    my $code = $2;
    $code =~ s/^(.*) \{\{ out \}\} .*$ /$1/mxs;
    my ($found) = grep { say "$language vs $_->[0]"; $language =~ /$_->[0]/xi } @{ $c->glots };
    if ($found) {
        $c->param(glot => $found->[1]);
    }
    $c->render(template => 'langtask', code => $code, page => $page, language => $language, glots => $c->glots);
} => 'langtask';

# thx: https://github.com/zoffixznet/perl6.party/blob/master/app.pl
post '/run' => sub {
    my $c = shift;
    my $code = $c->param('code') or return $c->reply->not_found;
    $code =~ s/\N{ZERO WIDTH SPACE}//g;
    my $glot_url = $c->param('glot');
    my $file = $c->param('filename');
    $c->ua->post(
        "$glot_url/latest" => {
            'Content-type'  => 'application/json',
            'Authorization' => 'Token ' . $c->config('glot_key')
        } => json => { files => [{ name => $file, content => $code }] }
        => sub($ua, $tx) {
            my $out = $tx->res->json;
            $out = trim join "\n", $out->{stdout} // (),
                $out->{stderr} ? "STDERR:\n$out->{stderr}" : ();
            $c->res->headers->content_type('text/plain');
            $c->render( text => $out );
        },
    );

    $c->render_later;
};


app->start;
__DATA__

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head><title><%= title %></title>
   %= stylesheet '/static/queens.css';
   %= stylesheet '/static/codemirror.css';
   %= stylesheet '//cdnjs.cloudflare.com/ajax/libs/foundation/6.2.3/foundation.min.css';
  </head>
  <body style='margin-left:5px;margin-right:5px;'>
  <div class='row'>
  <%= content %>
  </div>
  </body>
</html>


@@ index.html.ep
% title 'poly';
% layout 'default';
<ol>
% for my $t (task_names) {
<li><%= link_to 'task', { name => $t } => begin %><%= $t %><%= end %>
% }
</ol>

@@ task.html.ep
% title $name;
% layout 'default';
<h1><%= $name %></h1>
<a target="_blank" href="<%= task($name)->{fullurl} %>">rosettacode</a>
<br>
<iframe class='embed' src="<%= task($name)->{fullurl} %>">></iframe>
<ol>
% my $i = 0;
% for my $l (@$langs) {
<li><%= link_to 'langtask' => { lang => ++$i, task => $name } => begin %><%= $l %><%= end %>
% }
</ol>

@@ langtask.html.ep
% title $task;
% layout 'default';
<h1><%= $task %></h1>
<h2><%= $language %></h2>
%= form_for 'run', method => 'POST' => begin
<div class='small-3 columns'>
%= select_field 'glot', $glots;
</div>
<div class='small-3 columns'>
%= text_field 'filename' => "runme";
</div>
<textarea class='CodeMirror CodeMirror-linenumbers' name='code' style='border:1px solid black;'>
%= $code
</textarea>
<br>
%= submit_button 'run' => class => "large button";
%= end

@@ static/queens.css
iframe.embed {
    width:100%;
    height:500px;
    overflow:scroll;
}
